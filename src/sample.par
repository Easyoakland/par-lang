define listrgb =
  .item(.red!)
  .item(.green!)
  .item(.blue!)
  .empty!

define rgb = [value] value {
  red?   => .red!
  green? => .green!
  blue?  => .blue!
}

define zip = [list][seq] list begin {
  empty? => do {
    seq.close?
  } in .empty!

  item[x] xs => do {
    seq.next[y]
    let y = rgb(y)
  } in .item((x)(y)!) xs loop
}

define zip_rgbs = zip(listrgb)

define zip2 = [seq1][seq2] begin {
  close => do {
    seq1.close?
    seq2.close?
  } in !

  next => do {
    seq1.next[x]
    seq2.next[y]
  } in ((x)(y)!) loop
}

define concat = [left][right] left begin {
  empty?     => right
  item[x] xs => .item(x) xs loop
}


define tree_of_colors =
  .node
    (.node
      (.empty!)
      (.red!)
      (.empty!)!)
    (.green!)
    (.node
      (.node
        (.empty!)
        (.yellow!)
        (.empty!)!)
      (.blue!)
      (.empty!)!)!

define flatten_process_syntax = chan caller {
  caller[tree]

  let caller = chan return {
    tree begin {
      empty? => {
        return <> caller
      }

      node[left][value][right]? => {
        let caller = chan return { left loop }
        caller.item(value)
        let caller = chan return { right loop }
        return <> caller
      }
    }
  }

  caller.empty!
}

define flatten = [tree] chan yield {
  let yield = tree begin {
    empty? => yield

    node[left][value][right]? => do {
      let yield = left loop
      yield.item(value)
    } in right loop
  }

  yield.empty!
}

define flattened = flatten(tree_of_colors)


define true  = chan result { result.true! }
define false = chan result { result.false! }

define list_of_booleans = chan consumer {
  consumer
    .item(true)
    .item(false)
    .item(false)
    .item(true)
    .item(true)
    .item(false)
    .empty!
}

define reverse = chan caller {
  caller[list]

  let caller = chan return {
    list begin {
      empty? => {
        return <> caller
      }

      item[value] => {
        let caller = chan return { list loop }
        caller.item(value)
        return <> caller
      }
    }
  }

  caller.empty!
}

define reverse_bools = reverse(list_of_booleans)
